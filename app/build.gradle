/*
 * Copyright 2019-2020 by Heiko Sch√§fer <heiko@rangun.de>
 *
 *  This file is part of android-webvirus.
 *
 *  android-webvirus is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Lesser General Public License as
 *  published by the Free Software Foundation, either version 3 of
 *  the License, or (at your option) any later version.
 *
 *  android-webvirus is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public License
 *  along with android-webvirus.  If not, see <http://www.gnu.org/licenses/>.
 *
 *  Last modified 17.02.20 08:22 by heiko
 */

import com.github.spotbugs.SpotBugsTask

apply plugin: 'com.android.application'
apply plugin: 'com.github.spotbugs'

// Load keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

sourceSets {
    main {
        java.srcDirs = []
    }
}

android {

    def versionPropsFile = file('version.properties')
    def code = 0

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE']=code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

    } else {
        throw new GradleException("Could not read version.properties!")
    }

    signingConfigs {
        debug {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId "de.rangun.webvirus"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode code
        versionName "0.3 (build: " + (code-2990) + ")"
        signingConfig signingConfigs.debug
        resConfigs "de"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {

        release {
            buildConfigField "String", "COPYRIGHT_YEAR", "\"" + (new Date().format('yyyy')) + "\""
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            buildConfigField "String", "COPYRIGHT_YEAR", "\"" + (new Date().format('yyyy')) + "\""
            debuggable true
        }
    }
    flavorDimensions "version"
    productFlavors {
        a_hirnlos { // developer version
            dimension "version"
            applicationIdSuffix ".hirnlos"
            versionNameSuffix "-hirnlos"
        }
        b_hip { // user version
            dimension "version"
            applicationIdSuffix ".hip"
            versionNameSuffix "-hip"
        }
    }
    aaptOptions {
        cruncherEnabled true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'commons-codec:commons-codec:1.14'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.takisoft.fix:preference-v7:28.0.0.0'
    implementation 'net.sourceforge.jregex:jregex:1.2_01'
    implementation 'androidx.room:room-runtime:2.2.4'
    annotationProcessor 'androidx.room:room-compiler:2.2.4'
    api 'com.google.guava:guava:28.2-android'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android-process:2.2'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.0.0'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
    //androidTestImplementation 'androidx.test.ext:junit:1.1.2-alpha02'
    //androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    //androidTestImplementation 'androidx.test:rules:1.3.0-alpha02'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
    //androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0-alpha02'
}

spotbugs {
    toolVersion = '4.0.0'
    ignoreFailures = true
    reportsDir = file("$project.buildDir/findbugsReports")
    excludeFilter = file("${project.buildDir}/../../../findbugs-android-exclude.xml")
    effort = "max"
    //reportLevel = "high"
}

tasks.withType(SpotBugsTask) {
    dependsOn 'assembleA_hirnlosDebug'
    group = "verification"
    classes = files("$projectDir.absolutePath/build/intermediates/javac/a_hirnlosDebug/classes/")
    source = fileTree('src/main/java')
    classpath = files()
    reports {
        xml.enabled = false
        html.enabled = true
    }
}